plugins {
  id 'build.core'
  id 'maven-publish'
  id 'java-platform'
  id 'signing'
}

printer.info("${project.name}: import kotlin-publish-platform")

if (infos.publish.enablePublish) {
  publishing {
    publications {
      maven(MavenPublication) {
        from components.javaPlatform

        pom {
          name = project.name
          description = project.description
          url = infos.url
          inceptionYear = infos.inceptionYear
          scm {
            connection = "scm:git:${infos.url}.git"
            developerConnection = "scm:git:${infos.url}.git"
            url = infos.url
          }
          if (infos.licenses != null) {
            licenses {
              infos.licenses.forEach { each ->
                license {
                  name = each.name
                  url = each.url
                }
              }
            }
          }
          if (infos.developers != null) {
            developers {
              infos.developers.forEach { each ->
                developer {
                  email = each.email
                }
              }
            }
          }
        }
      }
    }

    repositories {
      mavenLocal()
      if (infos.publish.isSnapshot) {
        maven {
          name = infos.publish.snapshotId
          url = infos.publish.urls.snapshot
          credentials {
            username = getProperty("publish${name.capitalize()}Username")
            password = getProperty("publish${name.capitalize()}Password")
          }
        }
      }
      if (infos.publish.isRelease) {
        maven {
          name = infos.publish.releaseId
          url = infos.publish.urls.release
          credentials {
            username = getProperty("publish${name.capitalize()}Username")
            password = getProperty("publish${name.capitalize()}Password")
          }
        }
      }
    }

    if (infos.publish.isSigning) {
      signing {
        def signingName = infos.publish.signingId
        def signingKeyId = getProperty("signing${signingName.capitalize()}KeyId")
        def signingPassword = getProperty("signing${signingName.capitalize()}Password")
        def signingKey = utils.readFileAsString(getProperty("signing${signingName.capitalize()}KeyFile"), "UTF-8")
        useInMemoryPgpKeys(signingKeyId, signingKey, signingPassword)
        sign publishing.publications.maven
      }
    }
  }
}