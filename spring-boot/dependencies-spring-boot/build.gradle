plugins {
    id 'java-platform'
}

description = "Easy for Spring Boot: Dependencies Platform"

ext {
    libVersions = [
            kotlinVersion            : kotlinVersion,
            springBootVersion        : "2.2.4.RELEASE",
            commonsLangVersion       : "3.9",
            commonsCollectionsVersion: "4.4",
            commonsBeanutilsVersion  : "1.9.4",
            commonsCodecVersion      : "1.14",
            guavaVersion             : "28.2-jre",
            testngVersion            : "7.1.0",
    ]
}

apply from: repositoriesGradle

dependencies {
    constraints {
        api(project(":spring-boot:common-spring-boot"))

        api("org.jetbrains.kotlin:kotlin-bom:${libVersions.kotlinVersion}")

        api("org.springframework.boot:spring-boot-starter:${libVersions.springBootVersion}")
        api("org.springframework.boot:spring-boot-dependencies:${libVersions.springBootVersion}")
        api("org.springframework.boot:spring-boot-configuration-processor:${libVersions.springBootVersion}")

        api("org.apache.commons:commons-lang3:${libVersions.commonsLangVersion}")
        api("org.apache.commons:commons-collections4:${libVersions.commonsCollectionsVersion}")
        api("commons-beanutils:commons-beanutils:${libVersions.commonsBeanutilsVersion}")
        api("commons-codec:commons-codec:${libVersions.commonsCodecVersion}")
        api("com.google.guava:guava:${libVersions.guavaVersion}")

        api("org.testng:testng:${libVersions.testngVersion}")
        api("org.springframework.boot:spring-boot-starter-test:${libVersions.springBootVersion}")
    }
}

apply plugin: 'maven-publish'
apply plugin: 'signing'

publishing {
    publications {
        maven(MavenPublication) {
            from components.javaPlatform

            pom {
                name = project.name
                description = project.description
                url = "https://github.com/srclab-projects/easy-spring-boot"
                licenses {
                    license {
                        name = "The Apache License, Version 2.0"
                        url = "http://www.apache.org/licenses/LICENSE-2.0.txt"
                    }
                }
                developers {
                    developer {
                        email = "fredsuvn@163.com"
                    }
                }
                scm {
                    connection = "scm:git:https://github.com/srclab-projects/easy-spring-boot.git"
                    developerConnection = "scm:git:https://github.com/srclab-projects/easy-spring-boot.git"
                    url = "https://github.com/srclab-projects/easy-spring-boot"
                }
            }
        }
    }

    repositories {
        mavenLocal()

        maven {
            name 'sonatypeRepository'
            url 'https://oss.sonatype.org/service/local/staging/deploy/maven2/'
            credentials {
                username = "${sonatypeUserName}"
                password = "${sonatypePassword}"
            }
        }
        maven {
            name = 'sonatypeSnapshotRepository'
            url = 'https://oss.sonatype.org/content/repositories/snapshots/'
            credentials {
                username = "${sonatypeUserName}"
                password = "${sonatypePassword}"
            }
        }
    }

    signing {
        sign publishing.publications.maven
    }
}